buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}


plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.7'
    //id "net.minecraftforge.gradle.forge" version "2.0.2"
}

repositories {
    maven {
        name 'BR Repo'
        url "http://www.teambrmodding.com/maven2/"
    }
    maven { // The repo from which to get waila
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven { //JEI
        name 'DVS1 Maven FS'
        url "http://dvs1.progwml6.com/files/maven"
    }
}


apply plugin: 'net.minecraftforge.gradle.forge'

apply plugin: 'maven'

ext.configFile = file "gradle.properties"

String APIToken = ''
if (file('APIToken').exists()) {
    APIToken = new File('APIToken').text
}

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

//String build = System.getenv("BUILD_NUMBER")
String build = ""
if (project.hasProperty("teamcity")) {
    build = "${project.ext['build.vcs.number.NeoTech_HttpsGithubComTeamBRModdingNeoTechGitRefsHeadsMaster']}"
}

if (build == null || build == '') {
    version = "${config.minecraft_version}-${config.mod_version}"
} else {
    version = "${config.minecraft_version}-${config.mod_version}-" + build.substring(0,7)
}


group= "com.teambrmodding.neotech" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "Neotech"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {

    version = config.minecraft_version + "-" + config.forge_version

    replaceIn "Reference.java"
    replace "@VERSION@", "${config.mod_version}"

    runDir = "eclipse"
    useDepAts = true

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    //mappings = "snapshot_20141130"
    mappings = "${config.MAPPINGS}"
    makeObfSourceJar = false
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

processResources
{
    exclude '**/Thumbs.db'

    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"
    compile "mcp.mobius.waila:Waila:${config.WAILA_version}:dev"
    deobfCompile "mezz.jei:jei_${config.minecraft_version}:${config.JEI_version}"
    compile "com.teambr.bookshelf:Bookshelf-API:latest.release:dev"
    //compile group: "com.teambr.bookshelf", name: "Bookshelf-API", version: latest.integration, classifier: "dev", changing: true
}

task devJar(type: Jar, dependsOn: 'classes') {
    from(sourceSets.main.output) {
        include '**'
    }
    extension = 'jar'
    classifier = 'dev'
}

task sourceJarScala(type: Jar, dependsOn: "classes") {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives devJar
    archives sourceJarScala
}

uploadArchives {
    repositories {
        mavenDeployer {
            configuration = configurations.deployerJars
            //repository(url: System.properties['URL']) {
            repository(url: System.properties['URL']) {
                authentication(userName: System.properties['HTTPUSERNAME'], password: System.properties['HTTPPASSWORD'])
            }

            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName

                pom.project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description 'NeoTech'
                    url 'https://github.com/TeamBR-Modding/NeoTech'
                    scm {
                        url 'https://github.com/TeamBR-Modding/NeoTech'
                        connection 'scm:git:git@github.com/TeamBR-Modding/NeoTech.git'
                        developerConnection 'scm:git:git@github.com/TeamBR-Modding/NeoTech.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/TeamBR-Modding/NeoTech/issues'
                    }
                    licenses {
                        license {
                            name 'Creative Commons Attribution-NonCommercial-ShareAlike 4.0 International License'
                            url 'http://creativecommons.org/licenses/by-nc-sa/4.0/'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'Dyonovan'
                            name 'Dyonovan'
                            roles {
                                role 'developer'
                            }
                        }
                        developer {
                            id 'pauljoda'
                            name 'pauljoda'
                            roles {
                                role 'developer'
                            }
                        }
                    }
                }
            }
        }
    }
}

/*curseforge {
    apiKey = APIToken
    project {
        id = '228643'
        changelog = 'Changelog: https://github.com/TeamBR-Modding/NeoTech/wiki/Changelog'
        releaseType = 'beta'
        addGameVersion '1.10.2'
        addArtifact devJar
        relations {
            requiredLibrary 'bookshelf-api-library'
        }
    }
}*/
